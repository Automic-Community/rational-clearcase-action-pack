*** Settings ***
Default Tags     ALL     win    unix
Test Template     Move Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK     CLEARTOOL PATH                  Comment                       PName                                         Target PName                                  RETCODE    STATUS          REPORT MESSAGE
1.Successful rename element
                      [Setup]    Move element                    ${_ROSE_ELEMENT}              ${_ROSE_ELEMENT}_new
                      OK         ${_CLEARTOOL_PATH}              ${EMPTY}                      ${_ROSE_ELEMENT}_new                          ${_ROSE_ELEMENT}                              0          ${ENDED_OK}     Moved

2.Successful move element
                      [Setup]    Setup test with move element    ${_DIR_VOB}//test_dir_elem    ${_DIR_VOB}//test_rose_elem                   ${_DIR_VOB}//test_dir_elem//test_rose_elem
                      OK         ${_CLEARTOOL_PATH}              ${EMPTY}                      ${_DIR_VOB}//test_dir_elem//test_rose_elem    ${_DIR_VOB}//test_rose_elem                   0          ${ENDED_OK}     Moved

3.Fail with invalid cleartool path
                      NOK        ${_CLEARTOOL_PATH}_invalid      ${EMPTY}                      ${_ROSE_ELEMENT}                              ${_ROSE_ELEMENT}_new                          0          ${ENDED_NOK}    ERROR[Directory]

4.Fail with invalid element path name
                      NOK        ${_CLEARTOOL_PATH}              ${EMPTY}                      ${_ROSE_ELEMENT}_invalid                      ${_ROSE_ELEMENT}                              0          ${ENDED_NOK}    cleartool: Error: Pathname not found:

5.Fail move element to unchecked out dir
                      [Setup]    Uncheckout elements             ${_DIR_VOB}//test_dir_elem
                      NOK        ${_CLEARTOOL_PATH}              ${EMPTY}                      ${_DIR_VOB}//test_rose_elem                   ${_DIR_VOB}//test_dir_elem//test_rose_elem    0          ${ENDED_NOK}    because it is not checked out.

*** Keywords ***
Move Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${comment}    ${pname}    ${target_pname}    ${returnCode}
    ...    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CLEARCASE.PUB.ACTION.MOVE with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${comment}: A comment for the element(s) to move.
    ...    - ${pname}: Specifies the element or group of elements to be moved.
    ...    - ${target_pname}: Specifies the element or group of elements to be moved.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_MOVE}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_MOVE_COMMENT}    ${comment}
    Action Set    ${PRT_MOVE_PNAME}    ${pname}
    Action Set    ${PRT_MOVE_TARGET_PNAME}    ${target_pname}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}

Setup test with move element
    [Arguments]    ${dir_to_checkout}    ${pname}    ${target_pname}
    Checkout elements    ${dir_to_checkout}
    Move element    ${pname}    ${target_pname}
