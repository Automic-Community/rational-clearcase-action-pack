*** Settings ***
Default Tags      win unix
Test Template     Make Attribute Type Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK     CLEARTOOL PATH                Comment      Attribute value type    Attribute type      VOB tag            RETCODE    STATUS          REPORT MESSAGE
1.Successful          [Setup]    Remove type                   attype       integer_att_type        ${_VOB}
                      OK         ${_CLEARTOOL_PATH}            "Success"    integer                 integer_att_type    ${_VOB}            0          ${ENDED_OK}     Created attribute type

2.Successful with no specified attribute value type
                      [Setup]    Remove type                   attype       no_spe_att_type         ${_VOB}
                      OK         ${EMPTY}                      ${EMPTY}     ${EMPTY}                no_spe_att_type     ${_VOB}            0          ${ENDED_OK}     Created attribute type

3.Fail with invalid cleartool path
                      NOK        ${_CLEARTOOL_PATH}_invalid    ${EMPTY}     ${EMPTY}                test                ${_VOB}            0          ${ENDED_NOK}    ERROR[Directory]

4.Fail with invalid attribute type
                      NOK        ${_CLEARTOOL_PATH}            ${EMPTY}     _invalid                test                ${_VOB}            0          ${ENDED_NOK}    Unrecognized value type

5.Fail with invalid vob tag
                      NOK        ${_CLEARTOOL_PATH}            ${EMPTY}     ${EMPTY}                test                ${_VOB}_invalid    0          ${ENDED_NOK}    Unable to determine VOB for pathname

*** Keywords ***
Make Attribute Type Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${comment}    ${att_value_type}    ${att_type}    ${vob_tag}
    ...    ${returnCode}    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CLEARCASE.PUB.ACTION.MAKE_ATTR_TYPE with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${comment}: A comment for the make attribute type.
    ...    - ${att_value_type}: Type of attribute value. Possible values (integer | real | time | string | opaque)
    ...    - ${att_type}: Attribute type.
    ...    - ${vob_tag}: A vob tag where to make attribute type.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_MAKE_ATTR_TYPE}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_MAKE_ATTR_TYPE_COMMENT}    ${comment}
    Action Set    ${PRT_MAKE_ATTR_TYPE_VALUE_TYPE}    ${att_value_type}
    Action Set    ${PRT_MAKE_ATTR_TYPE_TYPE}    ${att_type}
    Action Set    ${PRT_MAKE_ATTR_TYPE_VOB_TAG}    ${vob_tag}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
