*** Settings ***
Default Tags     ALL     win    unix
Test Template     Start View Template
Resource          ../../resources/keywords.txt

*** Variables ***
# Avoid conflict with other testcases that are using "test_view"
${_VIEW}          myview

*** Test Cases ***    OK_NOK    CLEARTOOL PATH                View              RETCODE    STATUS          REPORT MESSAGE
1.Successful with normal case
                      OK        ${_CLEARTOOL_PATH}            ${_VIEW}          0          ${ENDED_OK}     ${EMPTY}

2.Successful with empty path
                      OK        ${EMPTY}                      ${_VIEW}          0          ${ENDED_OK}     ${EMPTY}

3.Fail with invalid path
                      NOK       ${_CLEARTOOL_PATH}_invalid    ${_VIEW}          0          ${ENDED_NOK}    ${EMPTY}

4.Fail with invalid view
                      NOK       ${_CLEARTOOL_PATH}            ${_VIEW}_wrong    0          ${ENDED_NOK}    ${EMPTY}

*** Keywords ***
Start View Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${view}    ${returnCode}    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action Start View with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${view}: View tag name.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    #Setup#
    End a view    ${_VIEW}
    Init Action    ${ACTION_STARTVIEW}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_STARTVIEW_VIEW}    ${view}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
