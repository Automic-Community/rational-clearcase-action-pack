*** Settings ***
Default Tags      win    unix
Test Template     Make Attribute Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK     CLEARTOOL PATH                Comment                Attribute type         Attribute value     Replace    Elements list               RETCODE    STATUS          REPORT MESSAGE
1.Successful with integer type
                      [Setup]    Remove attribute              ${_ATT_TYPE}           ${_HTML_ELEMENT}
                      OK         ${_CLEARTOOL_PATH}            "Success"              ${_ATT_TYPE}           123                 YES        ${_HTML_ELEMENT}            0          ${ENDED_OK}     Created attribute

2.Successful with string type
                      [Setup]    Remove attribute              ${_ATT_NO_SPE_TYPE}    ${_HTML_ELEMENT}
                      OK         ${EMPTY}                      ${EMPTY}               ${_ATT_NO_SPE_TYPE}    test                NO         ${_HTML_ELEMENT}            0          ${ENDED_OK}     Created attribute

3.Fail with invalid cleartool path
                      NOK        ${_CLEARTOOL_PATH}_invalid    "Success"              ${_ATT_TYPE}           123                 YES        ${_HTML_ELEMENT}            0          ${ENDED_NOK}    ERROR[Directory]

4.Fail with invalid element path name
                      NOK        ${_CLEARTOOL_PATH}            Success"               ${_ATT_TYPE}           123                 YES        ${_HTML_ELEMENT}_invalid    0          ${ENDED_NOK}    cleartool: Error: Pathname not found

5.Fail with invalid attribute type
                      NOK        ${_CLEARTOOL_PATH}            ${EMPTY}               _invalid               123                 YES        ${_HTML_ELEMENT}            0          ${ENDED_NOK}    cleartool: Error: attribute type

6.Fail with invalid attribute value
                      NOK        ${_CLEARTOOL_PATH}            ${EMPTY}               ${_ATT_TYPE}           _invalid_integer    YES        ${_HTML_ELEMENT}            0          ${ENDED_NOK}    Invalid integer value

*** Keywords ***
Make Attribute Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${comment}    ${att_type}    ${att_value}    ${replace}
    ...    ${list_elements}    ${returnCode}    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CLEARCASE.PUB.ACTION.MAKE_ATTR with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${comment}: A comment for the make attribute.
    ...    - ${att_type}: Specify an existing attribute type
    ...    - ${att_value}: Specifies the attribute's value
    ...    - ${replace}: Removes an existing attribute of the same type before attaching the new one, thus avoiding the collision. Valid values are YES or NO
    ...    - ${list_elements}: Elements to make attribute on.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_MAKE_ATTR}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_MAKE_ATTR_COMMENT}    ${comment}
    Action Set    ${PRT_MAKE_ATTR_TYPE}    ${att_type}
    Action Set    ${PRT_MAKE_ATTR_VALUE}    ${att_value}
    Action Set    ${PRT_MAKE_ATTR_REPLACE}    ${replace}
    Action Set    ${PRT_MAKE_ATTR_ELEMENTS}    ${list_elements}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
