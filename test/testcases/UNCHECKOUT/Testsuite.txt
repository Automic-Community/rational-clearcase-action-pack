*** Settings ***
Default Tags      win    unix
Test Template     Uncheckout Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK                              CLEARTOOL PATH                List of Elements            Keep    RETCODE    STATUS          REPORT MESSAGE
1.Successful with 1 element
    [Tags]  BASIC    
                      [Setup]                             Checkout elements             ${_FILE_ELEMENT}
                      OK                                  ${_CLEARTOOL_PATH}            ${_FILE_ELEMENT}            YES     0          ${ENDED_OK}     Checkout cancelled for
                      #2.Successful with multi element
                      #[Setup]                            Checkout elements             ${MULTI_ELEMENTS}
                      #OK                                 ${EMPTY}                      ${MULTI_ELEMENTS}           NO      0          ${ENDED_OK}     Checkout cancelled for

3.Fail with invalid cleartool path
                      NOK                                 ${_CLEARTOOL_PATH}_invalid    ${_FILE_ELEMENT}            NO      0          ${ENDED_NOK}    ERROR[Directory]

4.Fail with invalid element path name
                      NOK                                 ${_CLEARTOOL_PATH}            ${_FILE_ELEMENT}_invalid    NO      0          ${ENDED_NOK}    cleartool: Error: Element name not found

5.Fail with not checkout elements
                      [Setup]                             UnCheckout elements           ${_FILE_ELEMENT}
                      NOK                                 ${_CLEARTOOL_PATH}            ${_FILE_ELEMENT}            NO      0          ${ENDED_NOK}    cleartool: Error: Unable to find checked out version for

*** Keywords ***
Uncheckout Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${list_elements}    ${keep}    ${returnCode}    ${returnStatus}
    ...    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CLEARCASE.PUB.ACTION.UNCHECKOUT with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${list_elements}: File and directory elements to be unchecked out.
    ...    - ${keep}: Set to YES to specify the -keep option to preserve the checked out version in a private file. Valid values are YES or NO.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_UNCHECKOUT}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_UNCHECKOUT_LIST_ELEMENTS}    ${list_elements}
    Action Set    ${PRT_UNCHECKOUT_KEEP}    ${keep}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
