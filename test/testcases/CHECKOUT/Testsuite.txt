*** Settings ***
Default Tags     ALL     win    unix
Test Template     Checkout Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK                              CLEARTOOL PATH                Comment                                     List of Elements            RETCODE    STATUS          REPORT MESSAGE
1.Successful with 1 element
    [Tags]  BASIC    
                      [Setup]                             Uncheckout elements           ${_FILE_ELEMENT}
                      OK                                  ${_CLEARTOOL_PATH}            ${EMPTY}                                    ${_FILE_ELEMENT}            0          ${ENDED_OK}     Checked out
                      #2.Successful with multi element
                      #[Setup]                            Uncheckout elements           ${MULTI_ELEMENTS}
                      #OK                                 ${EMPTY}                      "Success checkout multi target elements"    ${MULTI_ELEMENTS}           0          ${ENDED_OK}     Checked out

3.Fail with invalid cleartool path
                      NOK                                 ${_CLEARTOOL_PATH}_invalid    ${EMPTY}                                    ${_FILE_ELEMENT}            0          ${ENDED_NOK}    ERROR[Directory]

4.Fail with invalid element path name
                      NOK                                 ${_CLEARTOOL_PATH}            ${EMPTY}                                    ${_FILE_ELEMENT}_invalid    0          ${ENDED_NOK}    No such file or directory

5.Fail with already checked out elements
                      [Setup]                             Checkout elements             ${_FILE_ELEMENT}
                      NOK                                 ${_CLEARTOOL_PATH}            ${EMPTY}                                    ${_FILE_ELEMENT}            0          ${ENDED_NOK}    is already checked out to view

*** Keywords ***
Checkout Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${comment}    ${list_elements}    ${returnCode}    ${returnStatus}
    ...    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CLEARCASE.PUB.ACTION.CHECKOUT with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${comment}: A comment for the checkout.
    ...    - ${list_elements}: File and directory elements to be checked out.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_CHECKOUT}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_CHECKOUT_COMMENT}    ${comment}
    Action Set    ${PRT_CHECKOUT_LIST_ELEMENTS}    ${list_elements}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}
