*** Settings ***
Default Tags      win    unix
Test Template     Remove View Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK     CLEARTOOL PATH                VIEW TAG              VIEW PATH        VIEW TYPE        RETCODE     STATUS          REPORT MESSAGE
1.Snapshot - Successful with normal case
                      [Setup]    Make View                     ${_CLEARTOOL_PATH}    ${VIEW_TEST1}    ${VIEW_PATH1}    snapshot
                      OK         ${_CLEARTOOL_PATH}            ${VIEW_TEST1}         ${VIEW_PATH1}    snapshot         0           ${ENDED_OK}     Created snapshot view directory

2.Snapshot - Successful with empty path
                      [Setup]    Make View                     ${_CLEARTOOL_PATH}    ${VIEW_TEST1}    ${VIEW_PATH1}    snapshot
                      OK         ${EMPTY}                      ${VIEW_TEST1}         ${VIEW_PATH1}    snapshot         0           ${ENDED_OK}     Created snapshot view directory

3.Dynamic - Successful with normal case
                      [Setup]    Make View                     ${_CLEARTOOL_PATH}    ${VIEW_TEST2}    ${VIEW_PATH2}    dynamic
                      OK         ${_CLEARTOOL_PATH}            ${VIEW_TEST2}         ${VIEW_PATH2}    dynamic          0           ${ENDED_OK}     Created view

4.Dynamic - Successful with empty path
                      [Setup]    Make View                     ${_CLEARTOOL_PATH}    ${VIEW_TEST2}    ${VIEW_PATH2}    dynamic
                      OK         ${EMPTY}                      ${VIEW_TEST2}         ${VIEW_PATH2}    dynamic          0           ${ENDED_OK}     Created view

5.Snapshot - Fail with invalid path
                      NOK        ${_CLEARTOOL_PATH}_invalid    ${VIEW_TEST1}         ${VIEW_PATH1}    snapshot         0           ${ENDED_NOK}    ${EMPTY}

6.Dynamic - Fail with invalid path
                      NOK        ${_CLEARTOOL_PATH}_invalid    ${VIEW_TEST1}         ${VIEW_PATH1}    dynamic          0           ${ENDED_NOK}    ${EMPTY}

*** Keywords ***
Remove View Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${view_tag}    ${view_path}    ${view_type}    ${returnCode}
    ...    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CLEARCASE.PUB.ACTION.REMOVE_VIEW with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${view_tag}: the view name to be created.
    ...    - ${view_path}: For a dynamic view, the dynamic view storage path or for a snapshot view, the path for the view.
    ...    - ${view_type}: Specifiy view type to make.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_REMOVE_VIEW}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_REMOVE_VIEW_VIEW_TAG}    ${view_tag}
    Action Set    ${PRT_REMOVE_VIEW_VIEW_PATH}    ${view_path}
    Action Set    ${PRT_REMOVE_VIEW_VIEW_TYPE}    ${view_type}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
