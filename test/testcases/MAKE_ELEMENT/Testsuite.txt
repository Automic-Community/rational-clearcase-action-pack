*** Settings ***
Default Tags      win unix
Test Template     Make Element Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK     CLEARTOOL PATH                                Comment                                          No Checkout    Element Type    Elements list                                    RETCODE    STATUS          REPORT MESSAGE
1.Successful          [Setup]    Setup test with remove checked out element    ${_ROSE_ELEMENT}_TC1
                      OK         ${_CLEARTOOL_PATH}                            "Success"                                        NO             rose            ${_ROSE_ELEMENT}_TC1                             0          ${ENDED_OK}     Created element

2.Successful with nocheckout = YES
                      [Setup]    Setup test with remove checked out element    ${_ROSE_ELEMENT}_TC2
                      OK         ${EMPTY}                                      ${EMPTY}                                         YES            rose            ${_ROSE_ELEMENT}_TC2                             0          ${ENDED_OK}     Created element

3.Successful with multi elements and nocheckout = YES
                      [Setup]    Setup test with remove checked out element    ${_ROSE_ELEMENT}_TC3_1 ${_ROSE_ELEMENT}_TC3_2
                      OK         ${EMPTY}                                      ${EMPTY}                                         YES            rose            ${_ROSE_ELEMENT}_TC3_1 ${_ROSE_ELEMENT}_TC3_2    0          ${ENDED_OK}     Created element

4.Fail with invalid cleartool path
                      NOK        ${_CLEARTOOL_PATH}_invalid                    "Fail"                                           NO             html            ${_HTML_ELEMENT}                                 0          ${ENDED_NOK}    ERROR[Directory]

5.Fail with invalid element path name
                      NOK        ${_CLEARTOOL_PATH}                            "Fail"                                           NO             html            _invalid${_HTML_ELEMENT}                         0          ${ENDED_NOK}    cleartool: Error: Unable to determine view for

6.Fail with existing element
                      NOK        ${_CLEARTOOL_PATH}                            ${EMPTY}                                         NO             _invalid        ${_HTML_ELEMENT}                                 0          ${ENDED_NOK}    cleartool: Error: Entry named "test_html_elem" already exists

7.Fail with invalid element type
                      NOK        ${_CLEARTOOL_PATH}                            "Fail"                                           NO             _invalid        ${_ROSE_ELEMENT}_TC6                             0          ${ENDED_NOK}    cleartool: Error: element type "_invalid" not found in VOB

*** Keywords ***
Make Element Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${comment}    ${no_checkout}    ${elem_type}    ${list_elements}
    ...    ${returnCode}    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CLEARCASE.PUB.ACTION.MAKE_ELEMENT with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${comment}: A comment for the make elements.
    ...    - ${no_checkout}: Set to YES to specify the -nco option, stops element from checkout after it was created.
    ...    - ${elem_type}: Specifies the attribute's value
    ...    - ${list_elements}: List of elements to be created
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_MAKE_ELEMENT}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_MAKE_ELEMENT_COMMENT}    ${comment}
    Action Set    ${PRT_MAKE_ELEMENT_NOCHECKOUT}    ${no_checkout}
    Action Set    ${PRT_MAKE_ELEMENT_TYPE}    ${elem_type}
    Action Set    ${PRT_MAKE_ELEMENT_ELEMENTS}    ${list_elements}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}

Setup test with remove checked out element
    [Arguments]    ${list_elements}
    UnCheckout elements    ${list_elements}
    Remove element    ${list_elements}
