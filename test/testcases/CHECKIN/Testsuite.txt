*** Settings ***
Suite Setup
Default Tags      win    unix
Test Template     Checkin Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK                                    CLEARTOOL PATH                    Comment                             List of Elements            Checkin identical    RETCODE    STATUS          REPORT MESSAGE
1.Successful Checkin
                      [Setup]                                   Setup test with Identical = NO
                      OK                                        ${_CLEARTOOL_PATH}                "Success checkin elements"          ${_HTML_ELEMENT}            NO                   0          ${ENDED_OK}     Checked in

2.Successful with Checkin Identical = YES
                      [Setup]                                   Checkout elements                 ${_HTML_ELEMENT}
                      OK                                        ${_CLEARTOOL_PATH}                ${EMPTY}                            ${_HTML_ELEMENT}            YES                  0          ${ENDED_OK}     Checked in
                      #3.Fail with checkin identical content
                      #[Setup]                                  Checkout elements                 ${_HTML_ELEMENT}
                      #NOK                                      ${_CLEARTOOL_PATH}                "Success checkin elements"          ${_HTML_ELEMENT}            NO                   0          ${ENDED_NOK}    cleartool: Error: By default, won't create version with data identical to predecessor.
                      #3.Successful Checkin multi elements
                      #[Setup]                                  Checkout elements                 ${MULTI_ELEMENTS}
                      #OK                                       ${_CLEARTOOL_PATH}                "Success checkin multi elements"    ${MULTI_ELEMENTS}           YES                  0          ${ENDED_OK}     Checked in

4.Fail with invalid cleartool path
                      NOK                                       ${_CLEARTOOL_PATH}_invalid        ${EMPTY}                            ${_HTML_ELEMENT}            NO                   0          ${ENDED_NOK}    ERROR[Directory]

5.Fail with invalid element path name
                      NOK                                       ${_CLEARTOOL_PATH}                ${EMPTY}                            ${_HTML_ELEMENT}_invalid    NO                   0          ${ENDED_NOK}    Element name not found

6.Fail with not checkout elements
                      [Setup]                                   Checkin elements                  ${_HTML_ELEMENT}
                      NOK                                       ${_CLEARTOOL_PATH}                ${EMPTY}                            ${_HTML_ELEMENT}            NO                   0          ${ENDED_NOK}    cleartool: Error: Unable to find checked out version for

*** Keywords ***
Checkin Template
    [Arguments]    ${OK_NOK}    ${cleartool_path}    ${comment}    ${list_elements}    ${identical}    ${returnCode}
    ...    ${returnStatus}    ${reportMessage}
    [Documentation]    This template used for testing action PCK.AUTOMIC_CLEARCASE.PUB.ACTION.CHECKIN with inputs
    ...    - ${cleartool_path}: Path to cleartool
    ...    - ${comment}: A comment for the checkin
    ...    - ${list_elements}: File and directory elements to be checked in.
    ...    - ${identical}: Set to YES to allow version files with identical content.
    ...    - ${returnCode}: return code of execution
    ...    - ${returnStatus}: return status of execution
    ...    - ${reportMessage}: message should be contained in execution report
    Init Action    ${ACTION_CHECKIN}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${cleartool_path}
    Action Set    ${PRT_CHECKIN_COMMENT}    ${comment}
    Action Set    ${PRT_CHECKIN_LIST_ELEMENTS}    ${list_elements}
    Action Set    ${PRT_CHECKIN_IDENTICAL}    ${identical}
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${reportMessage}

Setup test with Identical = NO
    Checkout elements    ${_HTML_ELEMENT}
    Add Text    ${_HTML_ELEMENT}    bla bla 123
