*** Settings ***
Documentation     This is high level keywords that will be used by local test cases
Resource          messages.txt
Resource          context.txt
Library           com.automic.robot.itpa.ItpaLibrary

*** Key Words ***
Init Action
    [Arguments]    ${actionName}
    Action Create    ${actionName}
    Action Set    ${PRT_AGENT}    ${_AGENT}
    Action Set    ${PRT_LOGIN}    ${_LOGIN}

Action Report Should Contain
    [Arguments]    ${text}
    ${report} =    Action Get Report
    Should Contain    ${report}    ${text}

Action Report Should Be Found
    ${report} =    Action Get Report
    Should Not Be Empty    ${report}

Action Report Should Not Be Found
    ${report} =    Action Get Report
    Should Be Empty    ${report}

Action Return Code Should Be
    [Arguments]    ${code}
    ${returnCode}=    Action Get Variable    Return code
    Should Be Equal    ${returnCode}    ${code}

Action Return Code Should Not Equal
    [Arguments]    ${code}
    ${returnCode}=    Action Get Variable    Return code
    Should Not Be Equal    ${returnCode}    ${code}

Action Return Status Should Be
    [Arguments]    ${status}
    ${returnStatus}=    Action Get Variable    Status
    Should Be Equal    ${returnStatus}    ${status}

Action Return Status Should Not Be
    [Arguments]    ${status}
    ${returnStatus}=    Action Get Variable    Status
    Should Not Be Equal    ${returnStatus}    ${status}

Action Return Variable Should Be
    [Arguments]    ${variableValue}
    ${out}=    Action Get Variable    ${variableName}
    Should Be Equal    ${out}    ${variableValue}

Action Return Variable Should Be Equal As Integers
    [Arguments]    ${variableValue}
    ${out}=    Action Get Variable    ${variableName}
    Should Be Equal As Integers    ${out}    ${variableValue}

UnCheckout elements
    [Arguments]    ${list_elements}
    [Documentation]    Keyword to uncheckout ${list_elements}
    Init Action    ${ACTION_UNCHECKOUT}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${_CLEARTOOL_PATH}
    Action Set    ${PRT_UNCHECKOUT_LIST_ELEMENTS}    ${list_elements}
    Action Set    ${PRT_UNCHECKOUT_KEEP}    NO
    Action Execute

Checkout elements
    [Arguments]    ${list_elements}
    [Documentation]    Keyword to checkout ${list_elements}
    Init Action    ${ACTION_CHECKOUT}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${_CLEARTOOL_PATH}
    Action Set    ${PRT_CHECKOUT_COMMENT}    ${EMPTY}
    Action Set    ${PRT_CHECKOUT_LIST_ELEMENTS}    ${list_elements}
    Action Execute

Add Text
    [Arguments]    ${text_file_name}    ${text_to_add}
    [Documentation]    Keyword to add text ${text_to_add} to file ${text_file_name}
    Init Action    PCK.AUTOMIC_TEXT.PUB.ACTION.ADD_TEXT
    Action Set    &UC4RB_TEXT_FILE_NAME#    ${text_file_name}
    Action Set    &UC4RB_TEXT_ADD_TEXT#    ${text_to_add}
    Action Execute

Checkin elements
    [Arguments]    ${list_elements}
    [Documentation]    Keyword to checkin ${list_elements}
    Init Action    ${ACTION_CHECKIN}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${_CLEARTOOL_PATH}
    Action Set    ${PRT_CHECKIN_LIST_ELEMENTS}    ${list_elements}
    Action Set    ${PRT_CHECKIN_IDENTICAL}    YES
    Action Execute

Start a view
    [Arguments]    ${view}
    Init Action    ${ACTION_GENERIC_CLEARTOOL}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${_CLEARTOOL_PATH}
    Action Set    ${PRT_GENERIC_CLEARTOOL_OPTIONS}    startview ${view}
    Action Execute
    Action Report Should Be Found

End a view
    [Arguments]    ${view}
    Init Action    ${ACTION_GENERIC_CLEARTOOL}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${_CLEARTOOL_PATH}
    Action Set    ${PRT_GENERIC_CLEARTOOL_OPTIONS}    endview ${view}
    Action Execute
    Action Report Should Be Found

Mount VOB
    [Arguments]    ${VOB_TAG}
    Init Action    ${ACTION_MOUNT_VOB}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${_CLEARTOOL_PATH}
    Action Set    ${PRT_MOUNT_VOB_TAG}    ${VOB_TAG}
    Action Execute

Unmount VOB
    [Arguments]    ${VOB_TAG}
    Init Action    ${ACTION_UNMOUNT_VOB}
    Action Set    ${PRT_CLEARTOOL_PATH}    ${_CLEARTOOL_PATH}
    Action Set    ${PRT_UNMOUNT_VOB_TAG}    ${VOB_TAG}
    Action Execute
